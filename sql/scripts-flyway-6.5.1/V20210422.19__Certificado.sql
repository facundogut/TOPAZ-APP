/****** Object:  View [dbo].[Certificado]    Script Date: 24/02/2021 17:56:43 ******/
DROP VIEW [dbo].[Certificado]
GO

/****** Object:  View [dbo].[Certificado]    Script Date: 24/02/2021 17:56:43 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE VIEW [dbo].[Certificado] 
									(
										COMPENSABLE,
										TRANSFERIBLE,
										LUGAR,
										DOMICILIOPAGO,
										DIA,
										MES,
										ANIO,
										VENCIMIENTODIA,
										VENCIMIENTOMES,
										VENCIMIENTOANIO,
										IMPORTE,
										INTERESES,
										TOTAL,
										IMPORTELETRAS,
										NOMINALLETRAS,
										PLAZO,
										CONTRACUENTA,
										MONEDA,
										JTS_OID,
										TASAEFECTIVA,
										MONEDALETRA,
										CLIENTE,
										PROVINCIA,
										NUMEROCERTIFICADO,
										NUMEROCERTLETRA,
										CODIGOCLI,
										CODIGOENTIDAD,
										NUMEROINSTURMENTO,
										CUENTA,
										GARANTIA,
										RETENCION,
										TASANOMINAL,
										COTIZACION,
										CODIGOESPECIE,
										VALORNOMINAL
										 )
AS
SELECT 
		CASE S.PRODUCTO WHEN (SELECT NUMERICO 
								FROM PARAMETROSGENERALES with (nolock)
								WHERE [CODIGO] = 707) THEN 'NO COMPENSABLE POR CAMARA'
							ELSE 'COMPENSABLE POR CAMARA'
							END AS COMPENSABLE,
		CASE P.TRANSFERIBLE
			WHEN 'N' THEN 'INSTRANSFERIBLE'
				ELSE 'TRANSFERIBLE' 
				END AS TRANSFERIBLE, 
		(SELECT DD.CALLE 
		FROM CLI_DIRECCIONES DD with (nolock) 
		WHERE S.SUCURSAL = DD.ID 
				AND DD.FORMATO = 'S' 
				AND DD.TIPODIRECCION = 'R'
		) AS LUGAR,
		(SELECT 'DOMICILIO DE PAGO: ' + DD.CALLE + ' ' + CONVERT(varchar(8), DD.NUMERO) + ' ' + DD.CPA_NUEVO + ' ' + UPPER(PPR.DESCRIPCION) + ' ARGENTINA'
		FROM CLI_DIRECCIONES DD with (nolock), CLI_PROVINCIAS PPr with (nolock) 
		WHERE S.SUCURSAL = DD.ID 
			AND DD.FORMATO = 'S' 
			AND DD.TIPODIRECCION = 'R' 
			AND PPr.DIM1 = DD.PROVINCIA
		) AS DOMICILIOPAGO,
		Day(S.C1620) AS DIA,
		CASE Month(S.C1620) 
				WHEN 1 THEN 'ENERO' WHEN 2 THEN 'FEBRERO'WHEN 3 THEN 'MARZO' WHEN 4 THEN 'ABRIL'
				WHEN 5 THEN 'MAYO' 	WHEN 6 THEN 'JUNIO'	WHEN 7 THEN 'JULIO'	WHEN 8 THEN 'AGOSTO'
				WHEN 9 THEN 'SEPTIEMBRE' WHEN 10 THEN 'OCTUBRE'	WHEN 11 THEN 'NOVIEMBRE' WHEN 12 THEN 'DICIEMBRE'
		END AS MES,
		Year(S.C1620) AS ANIO,
		day(S.C1627)  AS VENCIMIENTODIA,
		month(S.C1627)  AS VENCIMIENTOMES,
		year(S.C1627)  AS VENCIMIENTOANIO,
		 S.C1604 AS IMPORTE,
		S.C1608 AS INTERESES,
		S.C1604 + S.C1608 AS TOTAL,
		dbo.NUMEROALETRAS ((S.C1604+S.C1608),2) AS IMPORTELETRAS,
		dbo.NUMEROALETRAS ((P.VALORNOMINAL),2) AS NOMINALLETRAS,
		S.C1642 AS PLAZO,
		(SELECT SS.CUENTA 
		FROM SALDOS SS with (nolock) 
		WHERE SS.JTS_OID = S.C1665
		) AS CONTRACUENTA,
		M.C6401 AS MONEDA,
		S.JTS_OID,
		CAST(S.C1632 as DECIMAL(11,2)) AS TASAEFECTIVA,
		CASE (C6399) 
			WHEN 80 THEN 'EN PESOS ARGENTINOS'
			WHEN 999 THEN 'EN UNIDADES DE VALOR ADQUISITIVO ACTUALIZABLES POR CER (UVA)'
			WHEN 988 THEN 'EN UNIDADES DE VIVIENDA ACTUALIZABLES POR CER (UVI)'
		END AS MONEDALETRA,
		'CUENTA:' + CONVERT(varchar(15), C.[CODIGOCLIENTE]) + ' ' + C.NOMBRECLIENTE + ' ' + DC.TIPODOC +': ' + DC.NUMERODOC + ' ' + D.CALLE + ' '+ CONVERT(varchar(8), D.NUMERO)   AS CLIENTE,
		UPPER(Dp.NOMBREDEP + ' PROVINCIA DE ' + Pr.DESCRIPCION) AS PROVINCIA,
		dbo.AGREGOCEROS (P.CERTIFICADO_DPF,8) AS NUMEROCERTIFICADO,
		dbo.CERTIFICADOLETRAS (P.CERTIFICADO_DPF) AS NUMEROCERTLETRA,
		C.[CODIGOCLIENTE] AS [CODIGOCLI],
		P.CERTIFICADO_DPF AS [CODIGOENTIDAD],
		P.NUMEROINTRUSMENTO AS NUMEROINSTURMENTO,
		P.CUENTACERTIFICADO AS CUENTA,
		CASE(P.FONDOGARANTIA)
			WHEN 'N' THEN 'DEPÓSITO SIN GARANTIA'
			WHEN 'S' THEN 'DEPÓSITO CON GARANTIA'
		END AS GARANTIA,
		RETENCION,
		CASE C1642 WHEN 0 THEN
			CAST ((((POWER( ((((S.C1632 / 100) * S.C1642) / Prd.C6255) + 1), Prd.C6255 / 1)) - 1) * 100) AS DECIMAL(11,2)) 
			ELSE
			CAST ((((POWER( ((((S.C1632 / 100) * S.C1642) / Prd.C6255) + 1), Prd.C6255 / S.C1642)) - 1) * 100) AS DECIMAL(11,2)) 
		END AS TASANOMINAL,
		P.COTIZACION AS COTIZACION,
		P.[CODIGOESPECIE] AS [CODIGOESPECIE],
		P.VALORNOMINAL AS VALORNOMINAL
FROM SALDOS S with (nolock) 
	inner join PZO_SALDOS P with (nolock) on  S.JTS_OID = P.JTS_OID_SALDO
	inner join CLI_CLIENTES C with (nolock) on  C.[CODIGOCLIENTE] = S.C1803 
	inner join CLI_DIRECCIONES D with (nolock) on D.ID = C.[CODIGOCLIENTE]
	inner join VW_CLI_X_DOC DC with (nolock)on DC.[CODIGOCLIENTE] = S.C1803  
	inner join MONEDAS M with (nolock)on  S.MONEDA = M.C6399 
	inner join CLI_PROVINCIAS Pr with (nolock)on D.PROVINCIA = Pr.DIM1 
	inner join CLI_DEPARTAMENTOS Dp with (nolock)on  Dp.DEPARTAMENTO = D.DEPARTAMENTO  and Dp.PROVINCIA = Pr.DIM1 
	inner join PRODUCTOS Prd with (nolock) on S.PRODUCTO = Prd.C6250
WHERE	 
D.TIPODIRECCION = 'PR' 
		
GO


