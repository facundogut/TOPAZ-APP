-----------------
--CORRECCIÓN SP--
-----------------
EXECUTE('
ALTER PROCEDURE PA_CONV_REC_CAJAS_LIQ
	@P_ID_PROCESO  float(53),
	@P_DT_PROCESO  datetime2(0),
	@P_RET_PROCESO float(53)  OUTPUT,
	@P_MSG_PROCESO varchar(max)  OUTPUT
AS
BEGIN
	SET @P_RET_PROCESO = NULL
    SET @P_MSG_PROCESO = NULL
    
    --INSTANCIA VARIABLES
    DECLARE @id_numerador NUMERIC(10);
    DECLARE @id_cabezal NUMERIC(15);
    DECLARE @fecha_proceso DATETIME;
	DECLARE @contador NUMERIC(15);
	DECLARE @v_constante VARCHAR(1);
    
	SET @fecha_proceso = (SELECT FECHAPROCESO FROM PARAMETROS)
	SET @contador = 0
	
	--DECLARO TABLAS AUXILIARES--
  	DECLARE @Tabla_CONV_REC_CAJAS_LIQ TABLE(
	  	CANTIDAD_REGISTROS NUMERIC (7),
	  	TOTAL_IMPORTE_CARGO NUMERIC (15, 2),
		ID_CABEZAL NUMERIC (15, 0),
		CONVENIO NUMERIC (15, 0),
		NOM_CONVENIO VARCHAR(40),
		TOTAL_CARGO_ESPECIFICO DECIMAL(15,2)
	)
	
	--

	DECLARE @liq_existentes TABLE (
		CONVENIO NUMERIC(15)
	)
	
	--COMPLETO TABLAS AUXILIARES--
	INSERT INTO
		@Tabla_CONV_REC_CAJAS_LIQ
	SELECT
		COUNT(DR.ID_LINEA) AS CANTIDAD_REGISTROS, 
		SUM(ISNULL(DR.IMPORTE,0)) AS TOTAL_IMPORTE_CARGO, 
		CR.ID AS ID_CABEZAL,
		CR.CONVENIO,
		C.NomConvRec AS NOM_CONVENIO, 
		SUM(ISNULL(DR.TOTAL_CARGO_ESPECIFICO,0)) AS TOTAL_CARGO_ESPECIFICO
	FROM 
		REC_CAB_RECAUDOS_CAJA AS CR WITH (nolock)
	INNER JOIN REC_DET_RECAUDOS_CAJA AS DR WITH (nolock) ON
		DR.ID_CABEZAL = CR.ID
		AND ISNULL(DR.ESTADO,''V'') =''V''
		AND DR.TZ_LOCK = 0
	INNER JOIN CONV_CONVENIOS_REC AS C WITH (nolock) ON
		C.Id_ConvRec = CR.CONVENIO
		AND C.TZ_LOCK = 0
	WHERE
		CR.TZ_LOCK = 0
		AND CR.FECHACARGA = @fecha_proceso
		AND CR.ESTADO = ''I''
	GROUP BY
		CR.ID,
		CR.CONVENIO,
		C.NomConvRec
	
	--	
	
	INSERT INTO
		@liq_existentes
	SELECT
		DISTINCT(CONVENIO)
	FROM 
		REC_LIQUIDACION WITH (nolock) 
	WHERE 
		TZ_LOCK = 0 
		AND FECHA = @fecha_proceso 
		AND ESTADO = ''R''
	
	--PROCESO LOS REGISTROS--
	BEGIN TRANSACTION
	
	BEGIN TRY

		WHILE EXISTS (SELECT * FROM @Tabla_CONV_REC_CAJAS_LIQ)
		BEGIN
			--CONTADOR
			SET @contador = @contador + 1
			
			--CREO LIQUIDACION PARA CABEZALES CON DETALLES Y ACTUALIZO LOS DETALLES
			SET @id_cabezal = (SELECT TOP 1 ID_CABEZAL FROM @Tabla_CONV_REC_CAJAS_LIQ ORDER BY ID_CABEZAL)
			EXECUTE dbo.SP_GET_NUMERADOR_TOPAZ 45036, @id_numerador OUTPUT;
			--CREO LIQUIDACION
			INSERT INTO 
				REC_LIQUIDACION (
					ID_LIQUIDACION, 
					ESTADO, 
					CONVENIO, 
					CONVENIO_PADRE, 
					FECHA, 
					MONEDA, 
					TOTALREGISTROS, 
					TOTALIMPORTE, 
					NOMBRE_CONVENIO_PADRE, 
					TOTAL_CARGO_ESPECIFICO
				)
			SELECT TOP 1
				@id_numerador,
				''L'',
				TA.CONVENIO, 
				0, 
				@fecha_proceso, 
				(SELECT C6399 FROM MONEDAS WHERE C6403 = ''N''),
				TA.CANTIDAD_REGISTROS, 
				TA.TOTAL_IMPORTE_CARGO, 
				TA.NOM_CONVENIO,
				TA.TOTAL_CARGO_ESPECIFICO
			FROM @Tabla_CONV_REC_CAJAS_LIQ AS TA
			WHERE 
				TA.ID_CABEZAL = @id_cabezal
				AND TA.CANTIDAD_REGISTROS > 0
				AND TA.CONVENIO NOT IN (SELECT 
											CONVENIO 
										FROM @liq_existentes)
			
			--MODIFICAMOS EL ESTADO DE LOS DETALLES A PROCESADOS
			UPDATE DR
			SET
				DR.ESTADO = ''P''
			FROM
				REC_DET_RECAUDOS_CAJA AS DR WITH (nolock)
			INNER JOIN REC_CAB_RECAUDOS_CAJA AS CR WITH (nolock) ON
				CR.ID = DR.ID_CABEZAL
				AND CR.ESTADO = ''I''
				AND CR.TZ_LOCK = 0
			INNER JOIN @Tabla_CONV_REC_CAJAS_LIQ AS TA ON
				TA.ID_CABEZAL = CR.ID
				AND TA.ID_CABEZAL = @id_cabezal
				AND TA.CANTIDAD_REGISTROS > 0
				AND TA.CONVENIO NOT IN (SELECT 
											CONVENIO 
										FROM @liq_existentes)
			WHERE
				DR.ESTADO = ''V''
				AND DR.TZ_LOCK = 0

			--SET CAB ESTADO P CON ID_LIQUIDACION
			UPDATE CR
			SET
				CR.ESTADO = ''P'',
				CR.TOTALREGISTROS = TA.CANTIDAD_REGISTROS,
				CR.TOTALIMPORTE = TA.TOTAL_IMPORTE_CARGO
			FROM
				REC_CAB_RECAUDOS_CAJA AS CR WITH (nolock)
			INNER JOIN @Tabla_CONV_REC_CAJAS_LIQ AS TA ON
				TA.ID_CABEZAL = CR.ID
				AND TA.ID_CABEZAL = @id_cabezal
				AND TA.CANTIDAD_REGISTROS > 0
			WHERE
				CR.ESTADO = ''I''
				AND CR.TZ_LOCK = 0
			
			--ACTUALIZAMOS ID_LIQUIDACIÓN SÓLO SI SE PROCESARON REGISTROS
			UPDATE CR
			SET
				CR.ID_LIQUIDACION = @id_numerador
			FROM
				REC_CAB_RECAUDOS_CAJA AS CR WITH (nolock)
			INNER JOIN @Tabla_CONV_REC_CAJAS_LIQ AS TA ON
				TA.ID_CABEZAL = CR.ID
				AND TA.CONVENIO NOT IN (SELECT 
											CONVENIO 
										FROM @liq_existentes)
			WHERE
				CR.ESTADO = ''I''
				AND CR.ID = @id_cabezal
				AND CR.TZ_LOCK = 0

			--ELIMINO REGISTRO UTILIZADO DE LA TABLA AUXILIAR PARA PROCEDER CON EL SIGUIENTE
			DELETE FROM @Tabla_CONV_REC_CAJAS_LIQ WHERE ID_CABEZAL = @id_cabezal
		END
		
		IF @@TRANCOUNT > 0
			BEGIN
				COMMIT TRANSACTION
			END
		SET @P_RET_PROCESO = 1
		SET @P_MSG_PROCESO = CONCAT(''Creacion de Liquidaciones Funcionó correctamente, se liquidaron '',@contador,'' cabezales'')
		EXECUTE dbo.pkg_constantes$VarcharConstantes ''C_LOG_TIPO_INFORMACION'', @v_constante OUTPUT;

	END TRY

	BEGIN CATCH

		BEGIN
			ROLLBACK TRANSACTION
			SET @P_RET_PROCESO = ERROR_NUMBER()
			SET @P_MSG_PROCESO = ERROR_MESSAGE()
			EXECUTE dbo.pkg_constantes$VarcharConstantes ''C_LOG_TIPO_ERROR'', @v_constante OUTPUT;
		END

	END CATCH
	
	EXECUTE dbo.PKG_LOG_PROCESO$PROC_INS_LOG_PROCESO 
		@P_ID_PROCESO = @P_ID_PROCESO, 
		@P_FCH_PROCESO = @P_DT_PROCESO, 
		@P_NOM_PACKAGE = ''PA_CONV_REC_CAJAS_LIQ'', 
		@P_COD_ERROR = @P_RET_PROCESO, 
		@P_MSG_ERROR = @P_MSG_PROCESO, 
		@P_TIPO_ERROR = @v_constante
END
')