EXECUTE('
IF OBJECT_ID (''dbo.VW_TRANSFERENCIAS_ACUMULADAS'') IS NOT NULL
	DROP VIEW dbo.VW_TRANSFERENCIAS_ACUMULADAS
')
EXECUTE('
CREATE VIEW VW_TRANSFERENCIAS_ACUMULADAS
AS
WITH ACUMULADO_DIARIO
AS (
	SELECT
		T.OP_JTS_OID_CUENTA AS SALDO,
		T.OP_MONEDA AS MONEDA,
		SUM(T.OP_IMPORTE) AS ACUMULADO_DIARIO
	FROM VTA_HIST_TRANSFERENCIAS AS T WITH (nolock)
	WHERE
		T.OP_FECHA = (SELECT FECHAPROCESO FROM PARAMETROS WITH (nolock))
		AND ((T.TZ_LOCK < 100000000000000 OR T.TZ_LOCK >= 200000000000000)
			AND (T.TZ_LOCK < 300000000000000 OR T.TZ_LOCK >= 400000000000000)  
		)
	GROUP BY
		T.OP_MONEDA,
		T.OP_JTS_OID_CUENTA,
		T.OP_FECHA	
),

ACUMULADO_MENSUAL
AS (
	SELECT
		T.OP_JTS_OID_CUENTA AS SALDO,
		T.OP_MONEDA AS MONEDA,
		SUM(T.OP_IMPORTE) AS ACUMULADO_MENSUAL
	FROM VTA_HIST_TRANSFERENCIAS AS T WITH (nolock)
	WHERE
		YEAR(T.OP_FECHA) = (SELECT YEAR(FECHAPROCESO) FROM PARAMETROS WITH (nolock))
		AND MONTH(T.OP_FECHA) = (SELECT MONTH(FECHAPROCESO) FROM PARAMETROS WITH (nolock))
		AND ((T.TZ_LOCK < 100000000000000 OR T.TZ_LOCK >= 200000000000000)
			AND (T.TZ_LOCK < 300000000000000 OR T.TZ_LOCK >= 400000000000000)  
		)
	GROUP BY
		T.OP_MONEDA,
		T.OP_JTS_OID_CUENTA,
		MONTH(T.OP_FECHA),
		YEAR(T.OP_FECHA)
)

SELECT
	AM.SALDO,
	AM.MONEDA,
	ISNULL(SUM(AD.ACUMULADO_DIARIO), 0) AS IMPORTE_DIARIO,
	ISNULL(SUM(AM.ACUMULADO_MENSUAL), 0) AS IMPORTE_MENSUAL,
	CASE
		WHEN AM.MONEDA <> (SELECT MONNAC FROM PARAMETROS WITH (nolock))
			THEN ISNULL(SUM(AM.ACUMULADO_MENSUAL), 0)
		ELSE
			ISNULL(SUM(AM.ACUMULADO_MENSUAL) / (SELECT C6440 FROM MONEDAS WITH (nolock) WHERE C6399
				= (SELECT NUMERICO FROM PARAMETROSGENERALES WITH (nolock) WHERE CODIGO = 18)), 0)
	END AS MENSUAL_CONVERTIDO
FROM ACUMULADO_MENSUAL AS AM
LEFT JOIN ACUMULADO_DIARIO AS AD ON
	AD.SALDO = AM.SALDO
	AND AD.MONEDA = AM.MONEDA
GROUP BY
	AM.SALDO,
	AM.MONEDA
')