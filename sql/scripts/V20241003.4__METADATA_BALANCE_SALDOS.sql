EXECUTE('

CREATE OR ALTER function FN_RRII_BALANCE_SALDOS (@fch_proceso DATE) 
RETURNS TABLE 
AS 
RETURN
SELECT 
	LEFT(CONVERT(VARCHAR,CBR.CODIGO_INTERNO) + REPLICATE(''0'',6), 6) AS CUENTA,	
	-1 * SUM(CBR.SALDOFINMESMO) AS IMPORTE,
	CBR.MONEDA,
 	@fch_proceso AS FECHA 
FROM CO_BALANCE_RESULTADOS CBR WITH (NOLOCK)
INNER JOIN MONEDAS M WITH (NOLOCK) ON CBR.MONEDA = M.C6399
WHERE CBR.ID_BCE = 2
AND CBR.FECHA = @fch_proceso
AND CBR.TIPO_BALANCE = ''M''
GROUP BY CBR.CODIGO_INTERNO,
		 CBR.MONEDA,
		 CBR.FECHA
;
')

EXECUTE('

CREATE OR ALTER FUNCTION FN_RRII_ConvertirAMonNac (@montoMonOrigen DECIMAL (38,2) , @moneda INT, @fechaParametro DATE) RETURNS DECIMAL (38,2) 
AS
--- Devuelve el Monto ingresado convertido a moneda nacional

BEGIN
	DECLARE @MontoMonNac DECIMAL (38,2)
	
	SELECT @MontoMonNac = 
		CASE 
		WHEN @moneda = (SELECT MONNAC FROM PARAMETROS WITH(NOLOCK))  THEN
			CAST(@montoMonOrigen AS DECIMAL (38,2)) 
		ELSE 
			ISNULL(CAST(@montoMonOrigen *
					(SELECT CAST(H.TIPO_CAMBIO_OFICIAL AS DECIMAL (38,2))
					FROM HISTORICOTIPOSCAMBIO H WITH (NOLOCK)
					WHERE 
							H.MONEDA = @moneda AND
							H.FECHA_COTIZACION = (SELECT MAX(HTC.FECHA_COTIZACION) 
												FROM HISTORICOTIPOSCAMBIO HTC WITH (NOLOCK) 																
												WHERE HTC.MONEDA = @moneda AND
												HTC.FECHA_COTIZACION <= CONVERT(DATETIME,@fechaParametro,103)))
		AS DECIMAL (38,2)),0)
		
	END
	RETURN CAST(@MontoMonNac AS DECIMAL (38,2))
END;
')

