EXECUTE('
ALTER PROCEDURE dbo.SP_OBTENER_PRODUCTO_PRESTAMOS_POS
    @TASA NUMERIC(11, 7),
    @IMPORTE_SOLICITUD NUMERIC(15,2),
    @JTS_OID NUMERIC(10),
    @LINEA VARCHAR(2) OUTPUT,
    @IMPORTE_DISPONIBLE NUMERIC(20,2) OUTPUT,
    @RESULTADO NUMERIC(5) OUTPUT
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @RESULTADO_LINEA VARCHAR(5);
    DECLARE @IMPORTE_DISP NUMERIC(15,2);

    -- CTE para obtener la tasa base más reciente
    WITH CTE_TASA_BASE AS (
        SELECT
            TIPOTASABASE,
            MAX(VIGENCIADESDE) AS VIGENCIADESDE
        FROM TASASBASE WITH (NOLOCK)
        WHERE TZ_LOCK = 0
        GROUP BY TIPOTASABASE
    ),
    -- CTE para obtener los puntos máximos por producto y moneda
    CTE_TASAS_MAXIMAS AS (
        SELECT
            PRODUCTO,
            MONEDA,
            MAX(PUNTOS) AS PUNTOS
        FROM TASAS WITH (NOLOCK)
        WHERE TZ_LOCK = 0
        GROUP BY PRODUCTO, MONEDA
    )
    
    SELECT 
        @RESULTADO_LINEA = tp.LINEA_SCORING  
    FROM CRE_PRODUCTOSCANALDIGITAL pr WITH (NOLOCK)
    INNER JOIN PRODUCTOS prod WITH (NOLOCK) 
        ON prod.C6250 = pr.PRODUCTO
    INNER JOIN TOPESPRODUCTO tp WITH (NOLOCK) 
        ON tp.CODPRODUCTO = prod.C6250
    INNER JOIN CTE_TASA_BASE tb 
        ON tb.TIPOTASABASE = tp.TIPO_TASA_BASE
    INNER JOIN TASASBASE tb1 WITH (NOLOCK)
        ON tb1.TIPOTASABASE = tb.TIPOTASABASE 
        AND tb1.VIGENCIADESDE = tb.VIGENCIADESDE 
        AND tb1.TZ_LOCK = 0
    INNER JOIN CTE_TASAS_MAXIMAS t 
        ON t.PRODUCTO = prod.C6250 
        AND t.MONEDA = tp.MONEDA
    WHERE 
        pr.CANAL = ''PR'' AND pr.HABILITADO=''S''
        --AND pr.CANT_MAX_CUOTAS > 1
        AND ROUND((t.PUNTOS+tb1.TASA_CONVERTIDA),1)= @TASA
	GROUP BY tp.LINEA_SCORING;
	
	

    -- Manejo de resultados
    IF @RESULTADO_LINEA IS NOT NULL
    BEGIN
    	SELECT @IMPORTE_DISP = SUM(PC.MontoCalculado)
		FROM VW_PRODUCTOS_CONVENIOS_AH PC WITH(NOLOCK)
		INNER JOIN TOPESPRODUCTO TP WITH(NOLOCK) ON PC.Producto=tp.CODPRODUCTO AND PC.LineaScoring = TP.LINEA_SCORING
		WHERE PC.Canal = ''PR'' AND PC.JTS_OID = @JTS_OID AND PC.LineaScoring = @RESULTADO_LINEA
		
		SET @LINEA = @RESULTADO_LINEA;
		
		SET @IMPORTE_DISPONIBLE = ISNULL(@IMPORTE_DISP,0);
		
		IF(@IMPORTE_SOLICITUD>=@IMPORTE_DISPONIBLE)
		BEGIN
			SET @RESULTADO = 1;
		END
		ELSE
		BEGIN
			SET @RESULTADO = 0;
		END
			
    END
    ELSE
    BEGIN
        SET @RESULTADO = 0;
        SET @IMPORTE_DISPONIBLE = 0;
        SET @LINEA = '''';
    END
END
')